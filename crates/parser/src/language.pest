program = _{ SOI ~ statement* ~ EOI }

statement  = _{ expression }
expression = _{ import | macro | export | enum }

// Programming type info.
elp_type    = { !KEYWORD ~ IDENT ~ elp_generic? }
elp_generic = { "<" ~ elp_type ~ ("," ~ elp_type)* ~ ">" }
enum        = { "enum" ~ IDENT ~ "{" ~ enum_member ~ ("," ~ enum_member)* ~ "}" }
enum_member = { "." ~ IDENT ~ ("(" ~ elp_type ~ ("," ~ elp_type)* ~ ")")? }

// Imports and exports in modules.
import         =  { "import" ~ "{" ~ import_pattern ~ ("," ~ import_pattern)* ~ "}" ~ "from" ~ string }
import_pattern = _{ IDENT ~ ("as" ~ IDENT)? }
export         = _{ "export" ~ statement }

// Meta programming tooling.
macro = { "@" ~ IDENT ~ ("(" ~ elp_type ~ ")")? }

// helpers.
string = ${ "\"" ~ inner ~ "\"" }
inner  = @{ char* }
char   =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
number = @{
    "-"? ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

// Lexer tokens
IDENT      = @{ !number ~ !KEYWORD ~ (ASCII_ALPHANUMERIC+ | "_")* ~ ASCII_ALPHANUMERIC* }
COMMENT    = _{ "//" ~ (!"\n" ~ ANY)* }
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
KEYWORD    = _{ "import" | "export" | "enum" | "object" }
