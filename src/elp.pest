WHITESPACE = _{ " " | "\t" | NEWLINE }
Program    = _{ SOI ~ Expression* ~ EOI }

Expression = {
    Import
  | Declaration
  | Call
  | Function
  | Export
  | Array
  | MemberValue
  | Literal
  | IfElseChain
}

Literal = {
    StringLiteral
  | NumberLiteral
  | BoolLiteral
}

Binding           =  { "=" ~ Expression }
ValueArg          = _{ (Ident | Literal | Call) ~ Binding? ~ ","? }
IdentConstituents = @{ !Keywords ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
Ident             =  { IdentConstituents ~ ("." ~ IdentConstituents)* }
Import            =  { "import" ~ "{" ~ ValueArg+ ~ "}" ~ "from" ~ StringLiteral }
Call              =  { LifeCycleModifier? ~ Ident ~ ("(" ~ ValueArg* ~ ")")? ~ TypeDeclaration? ~ Body? }
Body              =  { "{" ~ Expression* ~ "}" }
Function          =  { FN ~ Ident ~ TypeDeclaration? ~ ("(" ~ Declaration* ~ ")")? ~ Body }
Export            =  { "export" ~ (Declaration | Function) }
Array             =  { "[" ~ (Expression ~ ",")* ~ "]" }
MemberValue       =  { "." ~ Ident ~ Binding? }
LifeCycleModifier =  { ASYNC | AWAIT }
ASYNC             =  { "async" }
AWAIT             =  { "await" }
TypeDeclaration   =  { ":" ~ Ident }
Declaration       =  { Mutability? ~ Ident ~ ("," ~ Ident)* ~ TypeDeclaration? ~ Binding? }
Mutability        =  { LET | CONST }
LET               =  { "let" }
CONST             =  { "const" }
FN                =  { "fn" }

BoolLiteral = { TRUE | FALSE }
TRUE        = { "true" }
FALSE       = { "false" }

StringLiteral         =  { "\"" ~ (StringLiteralInterior | escape)* ~ "\"" }
StringLiteralInterior =  { (!("\\" | "\"") ~ ANY)+ }
predefined            =  { "n" | "r" | "t" | "\\" | "0" | "\"" | "'" }
byte                  =  { "x" ~ hex{2} }
unicode               =  { "u" ~ "{" ~ unicode_hex ~ "}" }
escape                =  { "\\" ~ (predefined | byte | unicode) }
hex                   = _{ '0'..'9' | 'a'..'f' | 'A'..'F' }
unicode_hex           =  { hex{1, 6} }

digit         = _{ '0'..'9' }
Int           =  { digit ~ (digit | "_")* }
Float         =  {
    Int ~ "." ~ Int? ~ exp?
  | Int ~ exp
}
plus          =  { "+" }
minus         =  { "-" }
exp           =  { ^"e" ~ (plus | minus)? ~ Int }
NumberLiteral =  { Int | Float }

IF           = @{ "if" }
ELSE         = @{ "else" }
ELSEIF       = @{ "elseif" }
MATCH        =  { "match" }
LogicControl =  { IF | ELSE | ELSEIF | MATCH }
IfElseChain  =  { LogicControl ~ Expression? ~ "{" ~ Expression* ~ "}" }

Keywords = { ASYNC | LET | CONST | IF | ELSE | ELSEIF | MATCH | FN }
