Program = { SOI ~ Expression* ~ EOI }

// PARENT TYPES
Expression = {
    Literal
  | Interface
  | Enum
  | Import
  | VariableBinding
  | VariableDeclaration
  | Function
  | FunctionCall
}
// \PARENT TYPES

// LITERALS
Literal = {
    StringLiteral
  | NumberLiteral
  | BoolLiteral
}

BoolLiteral = { TRUE | FALSE }
TRUE        = { "true" }
FALSE       = { "false" }

StringLiteral         =  { "\"" ~ (StringLiteralInterior | escape)* ~ "\"" }
StringLiteralInterior =  { (!("\\" | "\"") ~ ANY)+ }
predefined            =  { "n" | "r" | "t" | "\\" | "0" | "\"" | "'" }
byte                  =  { "x" ~ hex{2} }
unicode               =  { "u" ~ "{" ~ unicode_hex ~ "}" }
escape                =  { "\\" ~ (predefined | byte | unicode) }
hex                   = _{ '0'..'9' | 'a'..'f' | 'A'..'F' }
unicode_hex           =  { hex{1, 6} }

digit         = _{ '0'..'9' }
Int           =  { digit ~ (digit | "_")* }
Float         =  {
    Int ~ "." ~ Int? ~ exp?
  | Int ~ exp
}
plus          =  { "+" }
minus         =  { "-" }
exp           =  { ^"e" ~ (plus | minus)? ~ Int }
NumberLiteral =  { Int | Float }
// \LITERALS

// BRANCHING
IF           = @{ "if" }
ELSE         = @{ "else" }
ELSEIF       = @{ "elseif" }
MATCH        =  { "match" }
LogicControl =  { IF | ELSE | ELSEIF }
IfElseChain  =  { LogicControl ~ Expression? ~ "{" ~ Expression* ~ "}" }
// \BRANCHING

// INTRINSICS
WHITESPACE = _{ " " | "\t" | NEWLINE }
IMPORT     =  { "import" }
FROM       =  { "from" }
INTERFACE  =  { "interface" }
ENUM       =  { "enum" }
FN         =  { "fn" }
CONST      =  { "const" }
Keywords   =  { IF | ELSE | ELSEIF | MATCH | INTERFACE | ENUM | IMPORT | FROM | CONST | FN }

IdentConstituents = @{ !Keywords ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
Ident             =  { IdentConstituents ~ ("." ~ IdentConstituents)* }
// \INTRINSICS

// DECLARATIONS
ValueBinding        = { "=" ~ Expression }
TypeBinding         = { ":" ~ Ident }
Member              = { "." ~ Ident ~ TypeBinding? ~ ValueBinding? }
EnumMember          = { "." ~ Ident ~ ParenArgDeclarations? }
Interface           = { INTERFACE ~ Ident ~ "{" ~ Member* ~ "}" }
Enum                = { ENUM ~ Ident ~ "{" ~ EnumMember+ ~ "}" }
VariableDeclaration = { CONST? ~ Ident ~ TypeBinding? }
Function            = { FN ~ Ident ~ ParenArgDeclarations? ~ FunctionInherits? ~ ReturnType? ~ Block }
// \DECLARATIONS

// STATEMENTS
Import          = { IMPORT ~ "{" ~ (Ident ~ ","?)+ ~ "}" ~ FROM ~ StringLiteral }
VariableBinding = { (VariableDeclaration ~ ","?)+ ~ ValueBinding }
// \STATEMENTS

// EXPRESSIONS
Block                = { "{" ~ Expression* ~ "}" }
ParenArgDeclarations = { ("(" ~ (Ident ~ TypeBinding ~ ","?)* ~ ")") }
CallArgDeclarations  = { (Ident ~ "=")? ~ Expression }
ReturnType           = { "->" ~ Ident }
FunctionInherits     = { ":" ~ FunctionCall }
FunctionCall         = { Ident ~ (("(" ~ CallArgDeclarations* ~ ")") | Block) }
// \EXPRESSIONS
